
pkgname='ppcoind'
pkgver=v0.4.0ppc.r4.g5ace24f
pkgrel=9
pkgdesc='Official CLI implementation of Peercoin, the sustainable and secure cryptocurrency alternative to Bitcoin.'
url='http://peercoin.net/'
license=('MIT')
install='ppcoind.install'
source=($pkgname'::git+https://github.com/ppcoin/ppcoin.git'
        'ppcoind.service'
        'peercoin.conf'
        'ppcoind.conf'
        'ppcoind.tmpfiles'
        'ppcoin-skel')

sha256sums=('SKIP'
            '4dcee7fd15834f4d4af6c2d37823f1ca2ef13c177aefc5b20c46fa9e1a02297d'
            '0bc7812785fa1844cdda1a2aecf393fa080d08a3e43801d2e8bf9ce1b2d6906e'
            '3b75d38d952aa5d3b83bcfdd84550a7851ed7e2053fd89a8ca16f9dbb4a0c3fa'
            '187ea10d4f1c7577a271513966fa255ec94e96b3212d16974beeaf81625ac24c'
            '39200e7dfce0d34fea6e624d1b716686c185f2df912a63fd5744262c0f3071a9')

arch=('armv6h' 'i686' 'x86_64')
makedepends=('boost' 'miniupnpc')
depends=('boost-libs' 'miniupnpc' 'openssl')

pkgver() {
 cd "$srcdir/$pkgname"
 git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
 cd "$srcdir/$pkgname"
 
 # make ppcoind
 make -f makefile.unix USE_UPNP=0 -C src CXXFLAGS="$CXXFLAGS"
 
}

package() {
 cd "$srcdir"
 install -Dm755 "$pkgname/src/ppcoind" "$pkgdir/usr/bin/ppcoind"
 install -Dm640 "peercoin.conf" "$pkgdir/etc/peercoin/peercoin.conf"
 install -Dm640 "ppcoind.conf" "$pkgdir/etc/conf.d/ppcoind.conf"
 install -Dm640 "ppcoin-skel" $pkgdir/etc/skel/ppcoind.conf
 install -Dm644 "ppcoind.service" "$pkgdir/usr/lib/systemd/system/ppcoind.service"
 install -Dm644 "ppcoind.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/ppcoind.conf"
 install -Dm644 "$pkgname/COPYING" "$pkgdir/usr/share/licenses/ppcoind/COPYING"
}
