
pkgname='ppcoind'
pkgver=v0.4.0ppc.r4.g5ace24f
pkgrel=12
pkgdesc='Official CLI implementation of Peercoin, the sustainable and secure cryptocurrency alternative to Bitcoin.'
url='http://peercoin.net/'
license=('MIT')
install='ppcoind.install'
source=($pkgname'::git+https://github.com/ppcoin/ppcoin.git'
        'ppcoind.service'
        'peercoin.conf'
        'ppcoind.conf'
        'ppcoind.tmpfiles'
        'ppcoin-skel')

sha256sums=('SKIP'
            '9eedec3cbc19718cd5428002a55e4ecb83fd5c85fd2c6cbe9bdab8304a1a7808'
            'd9787233db52b2738d4376cd04278365bbb43d0c7c08eac20d2f2378b12787d4'
            '5a412019c01368c2a6546f19b73f167620ec52a905f5dd914c00def3c4268e7b'
            '187ea10d4f1c7577a271513966fa255ec94e96b3212d16974beeaf81625ac24c'
            '39200e7dfce0d34fea6e624d1b716686c185f2df912a63fd5744262c0f3071a9')

arch=('armv6h' 'i686' 'x86_64')
makedepends=('boost' 'miniupnpc')
depends=('boost-libs' 'miniupnpc' 'openssl')
optdepends=('ppcoind-blockchain')

pkgver() {
 cd "$srcdir/$pkgname"
 git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
 cd "$srcdir/$pkgname"
 
 # make ppcoind
 make -f makefile.unix USE_UPNP=1 -C src CXXFLAGS="$CXXFLAGS"
 
}

package() {
 cd "$srcdir"
 install -Dm755 "$pkgname/src/ppcoind" "$pkgdir/usr/bin/ppcoind"
 install -Dm640 "peercoin.conf" "$pkgdir/etc/peercoin/peercoin.conf"
 install -Dm640 "ppcoind.conf" "$pkgdir/etc/conf.d/ppcoind.conf"
 install -Dm640 "ppcoin-skel" "$pkgdir/etc/skel/.ppcoin"
 install -Dm644 "ppcoind.service" "$pkgdir/usr/lib/systemd/system/ppcoind.service"
 install -Dm644 "ppcoind.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/ppcoind.conf"
 install -Dm644 "$pkgname/COPYING" "$pkgdir/usr/share/licenses/ppcoind/COPYING"
}
