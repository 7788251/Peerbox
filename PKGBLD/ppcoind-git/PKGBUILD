
pkgname='ppcoind'
pkgver=v0.4.0ppc.r4.g5ace24f
pkgrel=15
pkgdesc='Official CLI implementation of Peercoin, the sustainable and secure cryptocurrency alternative to Bitcoin.'
url='http://peercoin.net/'
license=('MIT')
install='ppcoind.install'
source=($pkgname'::git+https://github.com/ppcoin/ppcoin.git'
        'ppcoind.service'
        'peercoin.conf'
        'ppcoind.conf'
        'ppcoind.tmpfiles'
        'ppcoin-skel'
        'keys.patch')

sha256sums=('SKIP'
            '3b88fe40619bdf8b1ac74aa7d64de0de61c472dce575129578dd25bf7fb58b8e'
            'd9787233db52b2738d4376cd04278365bbb43d0c7c08eac20d2f2378b12787d4'
            '4a5969b3aba06fcbc435f5d0d2ec7f8aa3f1b5a81d09322b017c72acff5d57c1'
            '187ea10d4f1c7577a271513966fa255ec94e96b3212d16974beeaf81625ac24c'
            '39200e7dfce0d34fea6e624d1b716686c185f2df912a63fd5744262c0f3071a9'
            '3be3095f63d33d43c990204002ce75802775d1ce0b6cfb71a0621cedc96b68e5')

arch=('armv6h' 'i686' 'x86_64')
makedepends=('boost' 'miniupnpc')
depends=('boost-libs' 'miniupnpc' 'openssl')

pkgver() {

 cd "$srcdir/$pkgname"
 git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'

}

prepare() {
	cd "$srcdir/$pkgname/src"

	## https://github.com/ppcoin/ppcoin/pull/79	
	msg2 "Apply patch: guard against openssl's new strict DER checks"
	patch key.cpp < "${srcdir}/keys.patch"

}

build() {

 cd "$srcdir/$pkgname"
  # make ppcoind
 make -f makefile.unix USE_UPNP=1 USE_SSL=1 -e PIE=1 -C src

}

package() {
 cd "$srcdir"
 install -Dm755 "$pkgname/src/ppcoind" "$pkgdir/usr/bin/ppcoind"
 install -Dm640 "peercoin.conf" "$pkgdir/etc/peercoin/peercoin.conf"
 install -Dm640 "ppcoind.conf" "$pkgdir/etc/conf.d/ppcoind.conf"
 install -Dm640 "ppcoin-skel" "$pkgdir/etc/skel/.ppcoin"
 install -Dm644 "ppcoind.service" "$pkgdir/usr/lib/systemd/system/ppcoind.service"
 install -Dm644 "ppcoind.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/ppcoind.conf"
 install -Dm644 "$pkgname/COPYING" "$pkgdir/usr/share/licenses/ppcoind/COPYING"
}
