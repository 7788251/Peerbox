pkgname=gradm
_version=3.0
_timestamp=201407222118
pkgver=3.0.$_timestamp
pkgrel=1
pkgdesc="Administration utility for grsecurity's Role Based Access Control (RBAC)"
arch=(armv6h i686 x86_64)
url=https://grsecurity.net/
license=(GPL2)
depends=(pam)
makedepends=('flex=2.5.37-1')
source=(https://grsecurity.net/stable/$pkgname-$_version-$_timestamp.tar.gz
        https://grsecurity.net/stable/$pkgname-$_version-$_timestamp.tar.gz.sig
        learn_config
        policy)

sha256sums=('6c29274d63293540646be8c8c2c131654ec307b17674c25085b352305562e7e8'
            'SKIP'
            '704ea6ba7f748761735cbe1cf52ef04f53eab1a1e9ea1bdcb6abaaf4a641e44d'
            'c2fb8e696969530c4dc707c59cdc608df6c21d6226979dfeadef92c51deabc5e')

prepare() {
  cd $pkgname
  sed -i -e 's/^CFLAGS :=/CFLAGS +=/' -e 's:sbin:usr/bin:' Makefile
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  cp "$srcdir"/{learn_config,policy} "$pkgdir/etc/grsec"
  rm -r "$pkgdir/dev"
}
