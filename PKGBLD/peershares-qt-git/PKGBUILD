# Maintainer: <dx /dot/ 1178 at gmail /dot/ com>
#

pkgname='peershares-qt-git'
_gitname=Peershares
pkgver=0.1.0
pkgrel=2
pkgdesc="Inexpensive and decentralized ledger to be used by businesses for tracking share ownership and distributing dividends in an automated fashion."
arch=('x86_64' 'i686')
url="https://github.com/Peershares/Peershares"
license=('MIT')
provides=('peershares')
conflicts=('peershares-qt' 'peershares-git')
depends=('qt4' 'miniupnpc' 'boost-libs' 'qrencode')
makedepends=('git' 'boost' 'gcc' 'make' 'qrencode')
source=('git://github.com/Peershares/Peershares'
        'peershares.desktop')
install=peershares.install

sha256sums=('SKIP'
            'ba2f5090e77fab41b5ddffc102ae8d11a7260992fc855ba4c3d32142de80ac0b')

pkgver() {
	cd "$srcdir/$_gitname"
	# Use tag of the last commit, but removing the prefix
	git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd ${_gitname}/

	# and make qt gui
	qmake-qt4 USE_QRCODE=1 USE_UPNP=1
	make

}

package() {

	install -Dm644 peershares.desktop ${pkgdir}/usr/share/applications/peershares.desktop

	cd ${_gitname}
	install -Dm755 peershares ${pkgdir}/usr/bin/peershares
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	install -Dm644 src/qt/res/icons/peershares.png ${pkgdir}/usr/share/pixmaps/peershares.png
}

