
pkgname=filesystem-peerbox
pkgver=0.21
pkgrel=1
pkgdesc='Base filesystem'
arch=('any')
license=('GPL')
provides=('filesystem')
replaces=('filesystem')
install='filesystem.install'
makedepends=('asciidoc')
depends=('iana-etc')
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/hosts.allow' 'etc/hosts.deny' 
        'etc/ld.so.conf' 'etc/passwd' 'etc/shadow' 'etc/gshadow' 'etc/resolv.conf' 
        'etc/nsswitch.conf' 'etc/shells' 'etc/host.conf' 'etc/securetty' 'etc/profile')

source=('group' 'issue' 'nsswitch.conf' 'securetty' 'host.conf' 'ld.so.conf' 'hostname'
        'passwd' 'shadow' 'fstab' 'crypttab' 'hosts' 'hosts.allow' 'hosts.deny' 'motd' 
        'os-release' 'resolv.conf' 'shells' 'gshadow' 'profile' 'modprobe.d.usb-load-ehci-first'
        'archlinux.7.txt' 'locale.sh' '05-sysctl.conf')

sha256sums=('867bc18abb625044136beaa4b8d6a14e10a1ccf9f8bdcc358fc496dbfbd18e73'
            '6726c3bdcfdc037fdf9da2f3e7717d47dc3ed10d38956a34509ff06641379df3'
            '3cfbffdf732985d8a5cd5145cca2da338df53bf45cd097f30c194294492f16f1'
            '4a5ef3c8fe48e377284660376fcb8999328c27b85e7560fce3add920e7bef357'
            'f21f35e7d3083aeee19beb48478130d119b2572922a4889df98326d551540eda'
            '00d5d103837480d26cd350753382c3211df6bc304ee59ffaab130fad108f6a86'
            'f32c2faa1221ba88c579ce2898f79bc166b668f4e8090a75134e6af937be54a6'
            '44acefcc5ba949ab8d7c58cdc497114b10d55b61b2f0023f73676af63e9de6d8'
            'fce7a860f805e05e4727d106e74fda7fa0f46caa64257bef494ce04e253e953c'
            '92fecd0e399cee86204f86cd8171c4c8d30d110263dc521479d48627f5b20a69'
            'ea8c67a34a0a8a41beabe3ceb5733c9e8b84b27c569c11ddbfd8395fce99d8dd'
            '13d1f59c1d0860bf43da996e2b9a6ab2744791614241ed9a1376b7e7e30de3a8'
            '962cb10ee1c503316ce727cc7130270295b483a8358b48a0c31b5e6a72e7f37a'
            '380ad81c12e61efebb01d1169810ebf5fb4b786bfded83125ccdd9da7f30223c'
            '11e086c3ad8270785d150305a7323387f436c10b52a5757dc9548ef690780be2'
            'ca49eca679c7bae3183a631753b3c4fb38c2a62db2b9ceb273790141d731b6ca'
            '74d2be3dd69600bcf0da751a1804a386bf4ad143b76d79c336acaa5c600e7991'
            '6cff886fe9e23096d5f67b7857a747ebaf10431be5a2afc40236de5ad13a654f'
            '5b1f38ae3648954f0d6a95bf069aeff06c3418380359f588774b1a28c105f491'
            '0262cacc91cd309be0747e088d46a39f0b205392fbbafab96d767e48a3872ad0'
            '90fb123aa8c0176bc144322b585bb889181c437542ddbe546af43e11b95be6ab'
            '22f9ec6a8e42e7ea743f611cb34ee13d1f0b97e3d388f03c6fdca57886e0a163'
            '6555917d318a1ca46f6e19091753cc7564b2fb4148d9d17b5999f9045ef27649'
            '9136e5491ac8f934bf966c7156b99d12cd77ee0337a0241bff9ba496e4e8f225')
            
build() {
	cd ${srcdir}
	a2x -d manpage -f manpage archlinux.7.txt
}

package() {

	cd ${pkgdir}

	#
	# setup root filesystem
	#
	for d in boot dev etc home mnt usr var opt srv/http run; do
		install -d -m755 ${d}
	done
	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	# vsftpd won't run with write perms on /srv/ftp
	install -d -m555 -g ftp srv/ftp
    
    #
	# setup /etc
	#
	install -d etc/{ld.so.conf.d,skel,profile.d}
	for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf os-release passwd \
	    resolv.conf securetty shells profile hosts.allow hosts.deny hostname; do
		install -m644 ${srcdir}/${f} etc/
	done
	
	#
	# etc/sysctl.d/
	#
	install -d etc/sysctl.d
	install -m644 ${srcdir}/05-sysctl.conf etc/sysctl.d/
		
	#
	# mtab & crypttab
	#
	ln -s /proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} etc/
	done
	
	touch etc/arch-release
	
	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first usr/lib/modprobe.d/usb-load-ehci-first.conf
	install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh
    
    #
	# setup /var
	#
	for d in cache/man local opt log/old lib/misc empty; do
		install -d -m755 var/${d}
	done
	install -d -m1777 var/{tmp,spool/mail}
	# allow setgid games to write scores
	install -d -m775 -g games var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	#
	# setup /usr hierarchy
	#
	for d in bin include lib share/misc src; do
		install -d -m755 usr/${d}
	done
	for d in $(seq 8); do
		install -d -m755 usr/share/man/man${d}
	done

	#
	# add lib symlinks
	#
	ln -s usr/lib ${pkgdir}/lib
	[[ ${CARCH} = "x86_64" ]] && (
		ln -s usr/lib ${pkgdir}/lib64
		ln -s lib ${pkgdir}/usr/lib64
	)

	#
	# add bin symlinks
	#
	ln -s usr/bin ${pkgdir}/bin
	ln -s usr/bin ${pkgdir}/sbin
	ln -s bin ${pkgdir}/usr/sbin

	#
	# install archlinux(7) manpage
	#
	install -D -m644 ${srcdir}/archlinux.7 usr/share/man/man7/archlinux.7

	#
	# setup /usr/local hierarchy
	#
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/${d}
	done
	ln -s ../man usr/local/share/man
}
