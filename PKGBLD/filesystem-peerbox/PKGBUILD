pkgname=filesystem-peerbox
pkgver=0.24
pkgrel=3
pkgdesc='Base filesystem'
arch=('any')
license=('GPL')
replaces=('filesystem')
conflicts=('filesystem')
provides=('filesystem')
install='filesystem.install'
makedepends=('asciidoc')
depends=('iana-etc')
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/passwd' 'etc/shadow' 
		'etc/gshadow' 'etc/host.conf' 'etc/securetty')

source=('group' 'issue' 'nsswitch.conf' 'securetty' 'host.conf' 'ld.so.conf' 'hostname'
        'passwd' 'shadow' 'fstab' 'fstab-raspi' 'crypttab' 'hosts' 'motd' 'os-release'
        'shells' 'gshadow' 'profile' 'modprobe.d.usb-load-ehci-first' 'resolv.conf'
        'archlinux.7.txt' 'locale.sh')

sha256sums=('cee22355ed9f1a16267e7e8425da56c267ba8eeb96554a5730234c29b3bcc030'
            '48bdfada99beadcc369e97926d880f2179edfc79eee556bf74074c79f6b3f8c7'
            '3cfbffdf732985d8a5cd5145cca2da338df53bf45cd097f30c194294492f16f1'
            '4a5ef3c8fe48e377284660376fcb8999328c27b85e7560fce3add920e7bef357'
            'f21f35e7d3083aeee19beb48478130d119b2572922a4889df98326d551540eda'
            '00d5d103837480d26cd350753382c3211df6bc304ee59ffaab130fad108f6a86'
            'f32c2faa1221ba88c579ce2898f79bc166b668f4e8090a75134e6af937be54a6'
            'cc1964557b00da5a3abdd4f5e8c07efd1eab6a105ae03b59ef13ca9093939cd8'
            '3971caa4d18eca5a9922d36bee3745fb85511ac593a073dab7d11722a13221e0'
            '8883589b20f7dbe12c32b9d0f9a3cfaaafe26bfd99b44db765565ca09f4b0dc4'
            '92fecd0e399cee86204f86cd8171c4c8d30d110263dc521479d48627f5b20a69'
            'efcd458ab5f0ab369ab57ce07e2e9cf1d7521852f5337c282a7913cb37aa808f'
            '20f606330ebb14018798934ea7728f9b941497515edaeea89f1250ed07c9ffd8'
            '6d073dc69dc039fc1ebaab12c9ee32bf88634322c4fc97c6fb1a58a63cb00c05'
            '0a3f416ce45bc3ef3bc0ea5d556a555c17a51544d753104612d170b887f3d0c9'
            '6cff886fe9e23096d5f67b7857a747ebaf10431be5a2afc40236de5ad13a654f'
            '3f07de8973712d75e472ce787f45dbb9d527d23a01f9a86548e9afba5c005860'
            '0262cacc91cd309be0747e088d46a39f0b205392fbbafab96d767e48a3872ad0'
            '90fb123aa8c0176bc144322b585bb889181c437542ddbe546af43e11b95be6ab'
            '74d2be3dd69600bcf0da751a1804a386bf4ad143b76d79c336acaa5c600e7991'
            '22f9ec6a8e42e7ea743f611cb34ee13d1f0b97e3d388f03c6fdca57886e0a163'
            '6555917d318a1ca46f6e19091753cc7564b2fb4148d9d17b5999f9045ef27649')
         
lint() {
	# ensure that passwd is sync'd to shadow and group is sync'd to gshadow.
	local r=0

	local passwd shadow group gshadow

	for f in passwd shadow group gshadow; do
		mapfile -t "$f" < <(cut -d: -f1 "$f" | sort)
	done

	# we can cheat and do simple string comparison only because we can make some
	# assumptions about the data in these files
	if [[ ${passwd[*]} != "${shadow[*]}" ]]; then
		error 'passwd is not in sync with shadow!'
		r=1
	fi

	if [[ ${group[*]} != "${gshadow[*]}" ]]; then
		error 'group is not in sync with gshadow!'
		r=1
	fi

	return $r
}

build() {
	cd ${srcdir}
	lint
	a2x -d manpage -f manpage archlinux.7.txt
}

package() {

	cd ${pkgdir}

	#
	# setup root filesystem
	#
	for d in boot dev etc home mnt usr var opt srv/http run; do
		install -d -m755 ${d}
	done
	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	# vsftpd won't run with write perms on /srv/ftp
	install -d -m555 -g ftp srv/ftp
    
    #
	# setup /etc
	#
	install -d etc/{ld.so.conf.d,skel,profile.d}
	for f in group host.conf hosts issue ld.so.conf motd nsswitch.conf os-release passwd \
	    resolv.conf securetty shells profile hostname; do
		install -m644 ${srcdir}/${f} etc/
	done

	if [ "$CARCH" == "armv6h" ]; then
		install -m644 ${srcdir}/fstab-raspi etc/fstab
	else
		install -m644 ${srcdir}/fstab etc/fstab
	fi
	
	#
	# mtab & crypttab
	#
	ln -s /proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} etc/
	done
	
	touch etc/arch-release
	
	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first usr/lib/modprobe.d/usb-load-ehci-first.conf
	install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh
    
    #
	# setup /var
	#
	for d in cache local opt log/old lib/misc empty; do
		install -d -m755 var/${d}
	done
	install -d -m1777 var/{tmp,spool/mail}
	# allow setgid games to write scores
	install -d -m775 -g games var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	#
	# setup /usr hierarchy
	#
	for d in bin include lib share/misc src; do
		install -d -m755 usr/${d}
	done
	for d in $(seq 8); do
		install -d -m755 usr/share/man/man${d}
	done

	#
	# add lib symlinks
	#
	ln -s usr/lib ${pkgdir}/lib
	[[ ${CARCH} = "x86_64" ]] && (
		ln -s usr/lib ${pkgdir}/lib64
		ln -s lib ${pkgdir}/usr/lib64
	)

	#
	# add bin symlinks
	#
	ln -s usr/bin ${pkgdir}/bin
	ln -s usr/bin ${pkgdir}/sbin
	ln -s bin ${pkgdir}/usr/sbin

	#
	# install archlinux(7) manpage
	#
	install -D -m644 ${srcdir}/archlinux.7 usr/share/man/man7/archlinux.7

	#
	# setup /usr/local hierarchy
	#
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/${d}
	done
	ln -s ../man usr/local/share/man
}
