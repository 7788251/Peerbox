pkgname=lsb-release
pkgver=1.4
pkgrel=1
pkgdesc="LSB version query program"
arch=('any')
url="http://www.linuxbase.org/"
license=('GPL2')
depends=('bash')
source=(http://downloads.sourceforge.net/lsb/$pkgname-$pkgver.tar.gz
        lsb_release_description.patch
        )

sha256sums=('99321288f8d62e7a1d485b7c6bdccf06766fb8ca603c6195806e4457fdf17172'
            '8291fc3e44dc56834f466d95e0f68905bbfbb5a629349958aee27d6fb28651da')
            
build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 < "$srcdir/lsb_release_description.patch"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -dm755 "$pkgdir/etc"
  echo "LSB_VERSION=$pkgver" >> "$pkgdir/etc/lsb-release"
  echo "DISTRIB_ID=Peerbox" >> "$pkgdir/etc/lsb-release"
  echo "DISTRIB_RELEASE=0.21" >> "$pkgdir/etc/lsb-release"
  echo "DISTRIB_DESCRIPTION=\"Peerbox\"" >> "$pkgdir/etc/lsb-release"

  install -Dm 755 lsb_release "$pkgdir/usr/bin/lsb_release"
}
