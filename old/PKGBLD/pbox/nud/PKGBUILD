pkgname='nud'
_gitname="nubit"
pkgver=v1.1.0.r1.g5133046
pkgrel=3
pkgdesc="The World's First Stable Digital Currency, daemon."
url="https://nubits.com/"
license=('custom')
install='nud.install'
arch=('armv6h' 'i686' 'x86_64')
source=(git+https://bitbucket.org/JordanLeePeershares/nubit
        'nud.service'
        'nu.conf'
        'nud.conf'
        'nud.tmpfiles'
        'nu-skel'
        )

sha256sums=('SKIP'
            '763882ae53e1151fd7bbb54632eecc23eb9147324e637b74546935773117305b'
            '993489e77cdb07bd69bd47ef240dea6e5b5296a3d320957807c0ea2c85e6f488'
            '8727ed6574f61fc641402d6d8e190d4c9424c0d9019269701e45ac81ee9ff3eb'
            '5b2f44fcf99c6ca327aff3338b0b752a60b784f8e513db38e2d34c8366b54dd0'
            'bbe3e7f55f36999de336a19d66558dc9c621b257a42fc0ed5a093a6dcc32791b')


makedepends=('boost' 'miniupnpc')
depends=('boost-libs' 'miniupnpc' 'openssl')

pkgver() {

 cd "$srcdir/$_gitname"
 git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'

}

build() {

  ## Build daemon
  cd $srcdir/$_gitname/src
  make -f makefile.unix USE_UPNP=0 USE_SSL=1 -e PIE=1

}

package() {
	
 cd $srcdir
 install -Dm755 $_gitname/src/nud $pkgdir/usr/bin/nud
 install -Dm644 nud.service $pkgdir/usr/lib/systemd/system/nud.service
 install -Dm640 nu.conf $pkgdir/etc/nu/nu.conf
 install -Dm640 nud.conf $pkgdir/etc/conf.d/nud.conf
 install -Dm640 nu-skel $pkgdir/etc/skel/.nu/nu.conf
 install -Dm644 nud.tmpfiles $pkgdir/usr/lib/tmpfiles.d/nud.conf
 install -Dm644 $_gitname/COPYING $pkgdir/usr/share/licenses/$_gitname/COPYING

}
