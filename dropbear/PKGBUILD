pkgname=dropbear
pkgver=2014.63
pkgrel=1
pkgdesc="Lightweight replacement for sshd"
arch=('i686' 'x86_64' 'armv6h')
url="http://matt.ucc.asn.au/dropbear/dropbear.html"
license=('MIT')
depends=('zlib')
source=(http://matt.ucc.asn.au/$pkgname/releases/$pkgname-$pkgver.tar.bz2
        $pkgname.service
        ${pkgname}-key.service
	$pkgname.socket)

sha256sums=('595992de432ba586a0e7e191bbb1ad587727678bb3e345b018c395b8c55b57ae'
            'c60747000fe54be72cdd1d73f7ce4e2b2aab94a3295483da8817f54044533f4e'
            '94e701c540e67c7f256a1c9ed8bd577efd868928ecc2bfa7cce22437e00e832e'
            'efe7106cccbe098908bf617b9ee749b0c9d3d87bab5218d23f9dfd3387a32619')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  sed -i 's|usr/libexec/sftp|usr/lib/ssh/sftp|' options.h

  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  DESTDIR=${pkgdir} make install

  # Configuration files
  install -d ${pkgdir}/etc/$pkgname
  install -D -m644 ${srcdir}/$pkgname.service ${pkgdir}/usr/lib/systemd/system/$pkgname.service
  install -D -m644 ${srcdir}/${pkgname}-key.service ${pkgdir}/usr/lib/systemd/system/${pkgname}-key.service
  install -D -m644 ${srcdir}/$pkgname.socket ${pkgdir}/usr/lib/systemd/system/$pkgname.socket

  # License file
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

