pkgname=openssh-peerbox
pkgver=6.6p1
pkgrel=4
pkgdesc='Free version of the SSH connectivity tools'
provides=('openssh')
conflicts=('openssh' 'openssh-hpn')
url='http://www.openssh.org/portable.html'
license=('custom:BSD')
arch=('armv6h' 'i686' 'x86_64')
depends=('openssl' 'libedit')
source=("ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/openssh-${pkgver}.tar.gz"
        'curve25519pad.patch'
        'ssh-genkeys.service'
        'sshd.service'
        'sshd.pam'
	'sshd_config'
	)

sha256sums=('48c1f0664b4534875038004cc4f3555b8329c2a81c1df48db5c517800de203bb'
            '1d458d3a4337a59a05ba64989bbfbc9f6360b4c39c5404d57da4cbe2e87e9f3a'
            '14afb1f9de35e304d102734f5163d08950cdfeac05d3d2d708a6f571142844bf'
            'c5ed9fa629f8f8dbf3bae4edbad4441c36df535088553fe82695c52d7bde30aa'
            '64576021515c0a98b0aaf0a0ae02e0f5ebe8ee525b1e647ab68f369f81ecd846'
            '27d0874afc1f87036409e2972f4352e9c1e9bb2a727fdf043a4408fe524826ba')

backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config' 'etc/pam.d/sshd')

prepare() {
	cd "${srcdir}/openssh-${pkgver}"
	patch -p0 -i ../curve25519pad.patch
}

build() {
	cd "${srcdir}/openssh-${pkgver}"

	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/ssh \
		--sysconfdir=/etc/ssh \
		--with-libedit \
		--with-ssl-engine \
		--with-pam \
		--with-privsep-user=nobody \
		--with-md5-passwords \
		--with-pid-dir=/run \

	make
}

package() {
	
	## Use preconfigured sshd_config

	cp sshd_config ${srcdir}/openssh-${pkgver}

	cd "${srcdir}/openssh-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
	
	install -Dm644 ../ssh-genkeys.service "${pkgdir}"/usr/lib/systemd/system/ssh-genkeys.service
	install -Dm644 ../sshd.service "${pkgdir}"/usr/lib/systemd/system/sshd.service
	install -Dm644 ../sshd.pam "${pkgdir}"/etc/pam.d/sshd

	install -Dm755 contrib/findssl.sh "${pkgdir}"/usr/bin/findssl.sh
	install -Dm755 contrib/ssh-copy-id "${pkgdir}"/usr/bin/ssh-copy-id
	install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}"/usr/share/man/man1/ssh-copy-id.1

}
