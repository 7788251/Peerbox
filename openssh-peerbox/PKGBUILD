pkgname=openssh
pkgver=6.6p1
pkgrel=1
pkgdesc='Free version of the SSH connectivity tools'
provides=('openssh-peerbox')
conflicts=('openssh' 'openssh-hpn')
url='http://www.openssh.org/portable.html'
license=('custom:BSD')
arch=('armv6h' 'i686' 'x86_64')
depends=('openssl' 'libedit' 'ldns')
source=("ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'curve25519pad.patch'
        'sshdgenkeys.service'
        'sshd.service'
        'sshd.pam'
	'sshd_config'
	)

sha256sums=('48c1f0664b4534875038004cc4f3555b8329c2a81c1df48db5c517800de203bb'
            'SKIP'
            '1d458d3a4337a59a05ba64989bbfbc9f6360b4c39c5404d57da4cbe2e87e9f3a'
            '3e13c5698d298f0e7f17247a4c5c96f58de125d51ae7ef417884fb83f7cf7ec1'
            'c5ed9fa629f8f8dbf3bae4edbad4441c36df535088553fe82695c52d7bde30aa'
            '64576021515c0a98b0aaf0a0ae02e0f5ebe8ee525b1e647ab68f369f81ecd846'
	    'd9c745020e2b3c3dfde314486cc855d990afb1d94efa4a636e3d3f38b4c24623')

backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config' 'etc/pam.d/sshd')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p0 -i ../curve25519pad.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/ssh \
		--sysconfdir=/etc/ssh \
		--with-ldns \
		--with-libedit \
		--with-ssl-engine \
		--with-pam \
		--with-privsep-user=nobody \
		--with-md5-passwords \
		--with-pid-dir=/run \

	make
}

package() {
	
	## Use preconfigured sshd_config

	cp sshd_config ${srcdir}/${pkgname}-${pkgver}

	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
	
	install -Dm644 ../sshdgenkeys.service "${pkgdir}"/usr/lib/systemd/system/sshdgenkeys.service
	install -Dm644 ../sshd.service "${pkgdir}"/usr/lib/systemd/system/sshd.service
	install -Dm644 ../sshd.pam "${pkgdir}"/etc/pam.d/sshd

	install -Dm755 contrib/findssl.sh "${pkgdir}"/usr/bin/findssl.sh
	install -Dm755 contrib/ssh-copy-id "${pkgdir}"/usr/bin/ssh-copy-id
	install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}"/usr/share/man/man1/ssh-copy-id.1

}
