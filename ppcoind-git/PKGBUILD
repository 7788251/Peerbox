
pkgname='ppcoind'
pkgver=v0.4.0ppc.r3.g1f058b6
pkgrel=2
pkgdesc='Official CLI implementation of Peercoin, the sustainable and secure cryptocurrency alternative to Bitcoin.'
url='http://peercoin.net/'
license=('MIT')
install='ppcoind.install'
source=($pkgname'::git+https://github.com/ppcoin/ppcoin.git'
        'ppcoind.service'
        'ppcoind.conf'
        'ppcoind.tmpfiles')

sha256sums=('SKIP'
            '5629e4e9c5ff8f61fe87e798ddf7acdc259f7c714fb815ea7df60451a4ecfd48'
            '6c3b228184f06630b079e8b898f0251121db2c41362c7938ac1f73c2d0cc1fb7'
            '187ea10d4f1c7577a271513966fa255ec94e96b3212d16974beeaf81625ac24c')

arch=('armv6h' 'i686' 'x86_64')
makedepends=('boost' 'miniupnpc')
depends=('boost-libs' 'miniupnpc' 'openssl')

pkgver() {
 cd "$srcdir/$pkgname"
 git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
 cd "$srcdir/$pkgname"
 
 # make ppcoind
 make -f makefile.unix -C src  CXXFLAGS="$CXXFLAGS"
 
}

package() {
 cd "$srcdir"
 install -Dm755 "$pkgname/src/ppcoind" "$pkgdir/usr/bin/ppcoind"
 install -Dm640 "ppcoind.conf" "$pkgdir/etc/ppcoind/ppcoind.conf"
 install -Dm644 "ppcoind.service" "$pkgdir/usr/lib/systemd/system/ppcoind.service"
 install -Dm644 "ppcoind.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/ppcoind.conf"
 install -Dm644 "$pkgname/COPYING" "$pkgdir/usr/share/licenses/ppcoind/COPYING"
}
